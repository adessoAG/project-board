import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	ext {
		springBootVersion = '2.0.6.RELEASE'
		springVersion = '5.0.9.RELEASE'
		lombokVersion = '1.18.2'
		hsqlVersion = '2.4.1'
		mysqlJVersion = '8.0.13'
		keycloakVersion = '4.3.0.Final'
		springLdapVersion = '2.3.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'net.ltgt.apt' version '0.10'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt'

group = 'de.adesso'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {

	compile(
			[group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "${springBootVersion}"],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootVersion}"],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "${springBootVersion}"],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: "${springBootVersion}"]
	)
	compile(
			[group: 'org.keycloak', name: 'keycloak-spring-security-adapter', version: "${keycloakVersion}"],
			[group: 'org.keycloak', name: 'keycloak-spring-boot-starter', version: "${keycloakVersion}"]
	)
	compile(group: 'org.springframework.ldap', name: 'spring-ldap-core', version: "${springLdapVersion}")
	compile(group: 'mysql', name: 'mysql-connector-java', version: "${mysqlJVersion}")

	compileOnly(group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: "${springBootVersion}")
	compileOnly(group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}")

	runtime(group: 'org.springframework.boot', name: 'spring-boot-devtools', version: "${springBootVersion}")
	runtime(group: 'org.hsqldb', name: 'hsqldb', version: "${hsqlVersion}")

	testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBootVersion}")
	testCompile(group: 'org.springframework.security', name: 'spring-security-test', version: "${springVersion}")
	testCompile(group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}")

	apt(group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}")
}

task dockerBuild(dependsOn: build) {
	doFirst {
		delete 'docker/temp'
	}

	doLast {
		copy {
			from('docker') {
				include 'application.properties'

				filter(ReplaceTokens, tokens: [
						activeProfiles: System.getProperty('activeProfiles'),
						datasourceUrl: System.getProperty('datasourceUrl'),
						datasourceUsername: System.getProperty('datasourceUsername'),
						datasourcePassword: System.getProperty('datasourcePassword')
				])
			}

			from('docker') {
				include 'application-adesso-jira.properties'

				filter(ReplaceTokens, tokens: [
						jiraUrl: System.getProperty('jiraUrl'),
						jiraUsername: System.getProperty('jiraUsername'),
						jiraPassword: System.getProperty('jiraPassword')
				])
			}

			from('docker') {
				include 'application-adesso-ad.properties'

				filter(ReplaceTokens, tokens: [
				        ldapUrl: System.getProperty('ldapUrl'),
						ldapUsername: System.getProperty('ldapUsername'),
						ldapPassword: System.getProperty('ldapPassword'),
						ldapBase: System.getProperty('ldapBase')
				])
			}

			from('docker') {
				include 'application-adesso-keycloak.properties'

				filter(ReplaceTokens, tokens: [
						keycloakUrl: System.getProperty('keycloakUrl'),
						keycloakRealm: System.getProperty('keycloakRealm'),
						keycloakClient: System.getProperty('keycloakClient'),
						keycloakPrincipalAttr: System.getProperty('keycloakPrincipalAttr'),
						keycloakCorsEnabled: System.getProperty('keycloakCorsEnabled')
				])
			}

			from('docker') {
				include 'Dockerfile'

				filter(ReplaceTokens, tokens: [
						jarName: jar.archiveName
				])
			}

			from "${buildDir}/libs/${jar.archiveName}"

			into 'docker/temp'
		}
	}
}

clean.doFirst {
	delete 'docker/temp'
}
