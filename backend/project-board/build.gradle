import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	ext {
		springBootVersion = '2.0.7.RELEASE'
		keycloakVersion = '4.6.0.Final'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'net.ltgt.apt' version '0.10'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt'

group = 'de.adesso'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
	}
}

dependencies {
	compile(
			[group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-web'],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-security'],
			[group: 'org.springframework.boot', name: 'spring-boot-starter-mail']
	)
	compile(
			[group: 'org.keycloak', name: 'keycloak-spring-boot-starter']
	)
	compile(group: 'org.springframework.ldap', name: 'spring-ldap-core')
	compile(group: 'mysql', name: 'mysql-connector-java')

	compileOnly(group: 'org.springframework.boot', name: 'spring-boot-configuration-processor')
	compileOnly(group: 'org.projectlombok', name: 'lombok')

	runtime(group: 'org.springframework.boot', name: 'spring-boot-devtools')
	runtime(group: 'org.hsqldb', name: 'hsqldb')

	testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
	testCompile(group: 'org.springframework.security', name: 'spring-security-test')

	annotationProcessor(group: 'org.projectlombok', name: 'lombok')

	apt(group: 'org.projectlombok', name: 'lombok')
}

task dockerBuild(dependsOn: build) {
	doFirst {
		delete 'docker/temp'
	}

	doLast {
		copy {
			from('docker') {
				include 'application.properties'

				filter(ReplaceTokens, tokens: [
						datasourceUrl: System.getProperty('datasourceUrl'),
						datasourceUsername: System.getProperty('datasourceUsername'),
						datasourcePassword: System.getProperty('datasourcePassword')
				])
			}

			from('docker') {
				include 'application-adesso-jira.properties'

				filter(ReplaceTokens, tokens: [
						jiraUrl: System.getProperty('jiraUrl'),
						jiraUsername: System.getProperty('jiraUsername'),
						jiraPassword: System.getProperty('jiraPassword')
				])
			}

			from('docker') {
				include 'application-adesso-ad.properties'

				filter(ReplaceTokens, tokens: [
						ldapUrl: System.getProperty('ldapUrl'),
						ldapUsername: System.getProperty('ldapUsername'),
						ldapPassword: System.getProperty('ldapPassword'),
						ldapBase: System.getProperty('ldapBase')
				])
			}

			from('docker') {
				include 'application-adesso-keycloak.properties'

				filter(ReplaceTokens, tokens: [
						keycloakUrl: System.getProperty('keycloakUrl'),
						keycloakRealm: System.getProperty('keycloakRealm'),
						keycloakClient: System.getProperty('keycloakClient'),
				])
			}

			from('docker') {
				include 'Dockerfile'

				filter(ReplaceTokens, tokens: [
						jarName: jar.archiveName
				])
			}

			from "${buildDir}/libs/${jar.archiveName}"

			into 'docker/temp'
		}
	}
}

clean.doFirst {
	delete 'docker/temp'
}